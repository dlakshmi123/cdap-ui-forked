// Set up the application plugin so we can launch the Gateway from gradle
apply plugin: 'java'
apply plugin: 'application'
apply from: "$rootProject.projectDir/gradle/packaging.gradle"

dependencies {
  compile elastic([group: 'com.continuuity', name: 'data-fabric', version: "${version}", changing: true], "data-fabric")
  compile elastic([group: 'com.continuuity', name: 'data-fabric', classifier: 'api', version: "${version}", changing: true], "data-fabric")
  compile elastic([group: 'com.continuuity', name: 'security', version: "${version}", changing: true], "security")
  testCompile elastic([group: 'com.continuuity', name: 'data-fabric', classifier: 'test', version: "${version}", changing: true], "data-fabric")

  compile elastic([group: 'com.continuuity', name: 'app-fabric', classifier: 'api', version: "${version}", changing: true], "app-fabric")
  compile elastic([group: 'com.continuuity', name: 'app-fabric', version: "${version}", changing: true], "app-fabric")

  clover elastic([group: 'com.continuuity', name: 'app-fabric', classifier: 'api', version: "${version}", changing: true], "app-fabric")
  clover elastic([group: 'com.continuuity', name: 'data-fabric', classifier: 'api', version: "${version}", changing: true], "data-fabric")

  compile libraries.slf4j_api
  compile libraries.logback_core
  compile libraries.logback_classic
  compile libraries.google_guice

  testCompile libraries.mockito_core
  testCompile libraries.junit

  compile libraries.avro
  compile libraries.avro_ipc
  compile libraries.flume_ng_core
  compile libraries.flume_ng_sdk

  compile libraries.twill_discovery_api
  compile libraries.continuuity_netty_http
}

test {
    jvmArgs '-Xmx2048m'
}

/**
 * Exclude any config xml from jar task
 */
jar.exclude '*.xml'

/**
 * Use io.netty instead of org.jboss.netty
 */
configurations {
    all*.exclude group: 'org.jboss.netty', module: 'netty'
    all*.exclude group: 'asm', module: 'asm'
    all {
        // Need to enforce our version of guava, otherwise some other components with newer versions may take over
        resolutionStrategy.force libraries.google_guava
    }
}

/**
 * Excludes Tests that are part of test suite.
 */
test {
  exclude '**/handlers/**'
  exclude '**/tools/**'
  exclude '**/collector/**'
}
